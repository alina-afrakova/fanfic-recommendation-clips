;;;;;;;;;;;;;;;;;;;;;
; function-priority
;;;;;;;;;;;;;;;;;;;;;

(defglobal ?*fact-priority* = 999)

(deffunction next-fact-priority ()
   (bind ?*fact-priority* (- ?*fact-priority* 1))
   ?*fact-priority*)

;;;;;;;;;;;;
; TEMPLATES
;;;;;;;;;;;;

(deftemplate fanfic
	(slot name (type STRING))
    (slot author (type STRING))
    (slot description (type STRING))
    (slot finish-date (type STRING))
    (slot pages (type INTEGER) 
                (default 10)
                (range 1 10000))
    (slot fandom (type SYMBOL) 
                 (default гарри-поттер)
                 (allowed-values гарри-поттер сверхъестественное дневники-вампира ориджинал))
    (slot status (type SYMBOL) 
                 (default завершен)
                 (allowed-values в-процессе заморожен завершен))
	(slot rating (type SYMBOL) 
                 (default r)
                 (allowed-symbols g pg-13 r nc-17 nc-21))
    (slot category (type SYMBOL) 
                   (default гет)
                   (allowed-symbols джен гет слэш фемслэш другое смешанная))
    (slot original-text (type SYMBOL) 
                        (default да)
                        (allowed-symbols да нет))
    (multislot tags (type SYMBOL))
)

(deftemplate attribute
   (slot name (type SYMBOL))
   (slot priority (default-dynamic (next-fact-priority)))
   (slot question (type STRING))
   (slot multiple-choice (type SYMBOL) (default yes) (allowed-symbols yes no))
   (multislot options (type SYMBOL INTEGER))
)
   
(deftemplate response
   (slot attribute (type SYMBOL))
   (multislot values (type SYMBOL INTEGER))
)

;;;;;;;;;;;;
; FUNCTIONS
;;;;;;;;;;;;

(deffunction ncontains (?values ?options)
    (bind ?result TRUE)
    (foreach ?v ?values
        (bind ?result (and ?result (member$ ?v ?options))))
    (return ?result)
)

(deffunction check_mult (?multiple-choice ?answer)
    (switch ?multiple-choice
        (case yes then (return TRUE))
        (case no  then (return (<= (length$ ?answer) 1))))
)

(deffunction str-qv (?question ?multiple-choice ?values)
    (switch ?multiple-choice
        (case yes then (return (str-cat ?question " (" (str-replace (implode$ ?values) " " ", ") "): ")))
        (case no  then (return (str-cat ?question " (" (str-replace (implode$ ?values) " " "|") "): "))))
)

(deffunction ask-question (?question ?multiple-choice $?allowed-values)
    (printout t (str-qv ?question ?multiple-choice ?allowed-values))
    (bind ?answer (explode$ (str-replace (lowcase (readline)) "," " ")))
    ; (if (lexemep ?answer) then (bind ?answer (lowcase ?answer)))
    (bind ?str-err "Введен недопустимый ответ. Пожалуйста, выберите из предложенных вариантов.")
    (while (or (not (ncontains ?answer ?allowed-values))
               (not (check_mult ?multiple-choice ?answer))) do
        (printout t ?str-err crlf (str-qv ?question ?multiple-choice ?allowed-values))
        (bind ?answer (explode$ (str-replace (lowcase (readline)) "," " "))))
    (return ?answer)
)

(deffunction print-fanfic (?f)
    (println "   Название: " (fact-slot-value ?f name))
    (println "   Автор: " (fact-slot-value ?f author))
    (println "   Описание: " (fact-slot-value ?f description))
    (println "   Дата завершения: " (fact-slot-value ?f finish-date))
    (println "   Кол-во страниц: " (fact-slot-value ?f pages))
    (println "   Фандом: " (fact-slot-value ?f fandom))
    (println "   Статус: " (fact-slot-value ?f status))
    (println "   Рейтинг: " (upcase (fact-slot-value ?f rating)))
    (println "   Направленность: " (fact-slot-value ?f category))
    (println "   Оригинальный ли текст: " (fact-slot-value ?f original-text))
    (println "   Тэги: " (str-replace (implode$ (fact-slot-value ?f tags)) " " ", ") crlf)
)

;;;;;;;;
; RULES
;;;;;;;;

(defrule welcome
    (declare (salience 1000))
    =>
    (println)
    (println "############################################################")
    (println "        Приветствую Вас в системе подбора фанфиков!        ")
    (println "############################################################" crlf)
    (println "Вам предстоит ответить на несколько вопросов, некоторые из них допускают множественный ответ (варинаты указаны через ','),")
    (println "а некоторые - только один (варианты для таких вопросов перечислены через '|'). Для множественных - вводите ответы через пробел или запятую.")
    (println "Вопросы можно пропускать (Вам не важен ответ) просто нажав Enter." crlf "Итак, вопросы:" crlf)
)

(defrule ask-question
    ?a <- (attribute (name ?attribute)
                     (priority ?priority)
                     (question ?question)
                     (multiple-choice ?multiple-choice)
                     (options $?options))
   (not (attribute (priority ?p2&:(> ?p2 ?priority))))
   ; (not (response (attribute ?attribute)))
   =>
   (retract ?a)
   (bind ?response (ask-question ?question ?multiple-choice $?options))
   (assert (response (attribute ?attribute) (values ?response)))
)

(defrule del-fanfic-exlude-tags
    (declare (salience 20))
    (response (attribute exclude-tags) (values $?exclude-tags))
    ?f <- (fanfic (tags $?fanfic-tags))
    (test (and (neq (length$ ?exclude-tags) 0)
               (ncontains ?exclude-tags ?fanfic-tags)))
    =>
    (retract ?f)
)

(defrule del-fanfic-max-pages
    (declare (salience 15))
    (response (attribute max-pages) (values ?max-pages $?))
    ?f <- (fanfic (pages ?pages-num))
    (test (> ?pages-num ?max-pages))
    =>
    (retract ?f)
)

(defrule del-fanfic-no-attribute
    (declare (salience 10))
    (response (attribute ?attribute) (values $?values))
    (test (not (member$ ?attribute (create$ max-pages exclude-tags))))
    ?f <- (fanfic)
    (test (or  (and (deftemplate-slot-multip fanfic ?attribute)
                    (not (ncontains $?values (fact-slot-value ?f ?attribute))))
               (and (not (deftemplate-slot-multip fanfic ?attribute))
                    (neq (length$ $?values) 0)
                    (not (member$ (fact-slot-value ?f ?attribute) $?values)))))
    =>
    (retract ?f)
)

(defrule no-factics-left
    (not (exists (fanfic)))
    =>
    (println crlf "К сожалению, в моей базе данных нет подходящего для Вас фанфика :(" crlf)
    (halt)
)

(defrule recommendation
    (declare (salience -1000))
    (exists (fanfic))
    ; (bind ?fanfics (find-all-facts ((?f fanfic)) TRUE))
    ; (bind ?length (length$ ?fanfics))
    =>
    (println crlf "Могу порекомендовать Вам следующие фанфики:" crlf)
    (do-for-all-facts ((?f fanfic)) TRUE
        (print-fanfic ?f))
    (println)
    (halt)
)

;;;;;;;;
; FACTS
;;;;;;;;

(deffacts MAIN::fanfic-attributes
    (attribute (name max-pages)
               (question "Какое максимальное количество страниц готовы прочитать?")
               (multiple-choice no)
               (options 5 10 30 50 100 200 300 500 1000 2000 3000 5000 10000))
    (attribute (name fandom)
               (question "Какой фандом(-ы) предпочитаете?")
               (multiple-choice yes)
               (options гарри-поттер сверхъестественное дневники-вампира ориджинал))
    (attribute (name status)
               (question "Фанфик с каким статусом готовы читать?")
               (multiple-choice yes)
               (options в-процессе заморожен завершен))
    (attribute (name rating)
               (question "Какой рейтинг(-и) хотите?") 
               (multiple-choice yes)
               (options g pg-13 r nc-17 nc-21))
    (attribute (name category)
               (question "Какая направленность(-и) предпочтительна?")
               (multiple-choice yes)
               (options джен гет слэш фемслэш другое смешанная))
    (attribute (name original-text)
               (question "Хотите оригинальный текст (не перевод)?")
               (multiple-choice no)
               (options да нет))
    (attribute (name tags)
               (question "Какие тэги хотите добавить?")
               (multiple-choice yes)
               (options au ooc ангст hurt-comfort драма романтика любовь-ненависть от-врагов-к-возлюбленным отрицание-чувств сложные-отношения флафф юмор дружба мистика ожп омп попаданчество смерть-второстепенных-персонажей))
    (attribute (name exclude-tags)
               (question "Какие тэги хотите исключить?")
               (multiple-choice yes)
               (options au ooc ангст hurt-comfort драма романтика любовь-ненависть от-врагов-к-возлюбленным отрицание-чувств сложные-отношения флафф юмор дружба мистика ожп омп попаданчество смерть-второстепенных-персонажей))
)

(deffacts MAIN::fanfic-list
    (fanfic (name "Платина и шоколад")
            (author "Чацкая")
            (description "Когда от усталости хочется содрать с себя шкуру, приходит спасение. В ненависти и ярости. В хронической злобе. В возвращающейся боли. И осознании: любое спасение временно.")
            (finish-date "17.12.2013")
            (pages 852)
            (fandom гарри-поттер)
            (status завершен)
            (rating nc-17)
            (category гет)
            (original-text да)
            (tags hurt-comfort ангст драма любовь-ненависть от-врагов-к-возлюбленным отрицание-чувств проклятия психология смерть-второстепенных-персонажей)
    )
    (fanfic (name "Это просто невозможно!")
            (author "Stella1408")
            (description "Драко Малфой в недоумении - Гарри Поттер совершенно перестал обращать на него внимание! На что готов Малфой, чтобы возродить былую искру ненависти в знаменитом гриффиндорце? И чем это обернётся для него самого? В общем, флафф, немного юмора и бедный Гарри =)")
            (finish-date "11.10.2012")
            (pages 119)
            (fandom гарри-поттер)
            (status завершен)
            (rating nc-17)
            (category слэш)
            (original-text да)
            (tags au ooc любовь-ненависть неозвученные-чувства отрицание-чувств нецензурная-лексика от-врагов-к-возлюбленным первый-раз подростковая-влюбленность сложные-отношения флафф юмор)
    )
    (fanfic (name "Скованные/Manacled")
            (author "Ekaterina Dunenkova, SenLinYu")
            (description "Гарри Поттер мертв. После окончания войны, чтобы укрепить свою власть, Волдеморт прибегает к программе повышения рождаемости и возрождения магического населения. У Гермионы Грейнджер есть тайна, скрытая и защищённая ее разумом, которая ставит под угрозу существование нового режима. Поэтому ее, как пленницу, отправляют к Верховному Правителю, чтобы взять под контроль и использовать в качестве суррогатной матери. Пока ее разум наконец не будет сломлен.")
            (finish-date "30.12.2021")
            (pages 1051)
            (fandom гарри-поттер)
            (status завершен)
            (rating nc-21)
            (category гет)
            (original-text нет)
            (tags au hurt-comfort ангст антиутопия война воспоминания беременность дарк драма жертвы-обстоятельств отклонения-от-канона победа-волдеморта повседневность потеря-памяти развитие-отношений серая-мораль сложные-отношения слоуберн xороший-плохой-финал смерть-второстепенных-персонажей)
    )
    (fanfic (name "Экскурсия по Лондону")
            (author "tuuli-veter")
            (description "— Если вы считаете, что мне самому это доставляет удовольствие... Вести стадо баранов приобщаться к культуре… — Снейп брезгливо оглядел всех своих подопечных и рявкнул: — Поттер, сядьте вы уже, наконец, рядом с вашим Малфоем и не маячьте. — Каким еще “вашим”? — мгновенно вскинулся Драко. — Чего это он вдруг мой? — одновременно с ним ощетинился Гарри.")
            (finish-date "03.11.2016")
            (pages 35)
            (fandom гарри-поттер)
            (status завершен)
            (rating pg-13)
            (category слэш)
            (original-text да)
            (tags au ooc любовь-ненависть первый-раз романтика флафф)
    )
    (fanfic (name "Большая игра")
            (author "Alteara")
            (description "В каком фэндоме вы хотите сыграть? Выберите свой пол. Выберите статус крови. Выберите мировоззрение. Создание персонажа завершено. Приятной игры!")
            (finish-date "05.04.2023")
            (pages 604)
            (fandom гарри-поттер)
            (status в-процессе)
            (rating nc-17)
            (category джен)
            (original-text да)
            (tags au ooc вымышленные-существа геймлит драма дружба мистика ожп омп попаданчество смерть-второстепенных-персонажей)
    )
    (fanfic (name "Стать Волан-де-Мортом")
            (author "Aniklin")
            (description "32-летний Гарри Поттер является ведущим артефактором магмира, а также Лордом Певерелл. В ходе магло-магической войны, спровоцированной выжившим Альбусом Дамблдором, Поттер умирает и попадает в прошлое, в своё 11-летнее тело. Он собирается во что бы то ни стало остановить старого манипулятора, и для этого ему придётся стать Волан-де-Мортом.")
            (finish-date "02.01.2019")
            (pages 81)
            (fandom гарри-поттер)
            (status завершен)
            (rating g)
            (category джен)
            (original-text да)
            (tags au ooc попаданчество учебные-заведения фэнтези экшн смерть-второстепенных-персонажей)
    )
    (fanfic (name "No Good Deed")
            (author "Laziest, Here'sTo")
            (description "Когда от усталости хочется содрать с себя шкуру, приходит спасение. В ненависти и ярости. В хронической злобе. В возвращающейся боли. И осознании: любое спасение временно.")
            (finish-date "14.11.2018")
            (pages 339)
            (fandom гарри-поттер)
            (status завершен)
            (rating nc-17)
            (category фемслэш)
            (original-text нет)
            (tags ангст драма любовь-ненависть насилие фэнтези)
    )
    (fanfic (name "Beneath Your Bones, Where I Reside")
            (author "Марлюшка, wolfrock")
            (description "Кастиэль слышит молитву Дина на свалке автомобилей и идёт на риск, чтобы показать ему, что Голод ошибался, говоря, что его душа сломана, а именно - касается его души своей благодатью")
            (finish-date "17.05.2013")
            (pages 5)
            (fandom сверхъестественное)
            (status завершен)
            (rating nc-17)
            (category слэш)
            (original-text нет)
            (tags hurt-comfort романтика фетиши)
    )
    (fanfic (name "Timeless")
            (author "Марлюшка, wolfrock")
            (description "Девушки решают переместиться назад во времени, чтобы убить Клауса задолго до того, как он станет неуязвим. Но все оказывается совсем не так просто, как они рассчитывали, и Кэролайн застревает в прошлом.")
            (finish-date "14.02.2014")
            (pages 269)
            (fandom дневники-вампира)
            (status завершен)
            (rating nc-17)
            (category гет)
            (original-text нет)
            (tags вымышленные-существа романтика)
    )
    (fanfic (name "Seduction")
            (author "Nikky Black, DestinyIgnites")
            (description "- Лорд Никлаус. Клаус отворачивается от горящего камина к двум его слугам, удерживающим молодую девушку. На улице холодная буря, и, будучи не в настроении для охоты, он послал на нее слуг, принесших ему сейчас эту маленькую блондинку. Вся дрожа, она поднимает на него наполненные страхом синие глаза. Он тепло ей кивает, знаю, что она будет мертва к исходу ночи.")
            (finish-date "06.01.2014")
            (pages 65)
            (fandom дневники-вампира)
            (status завершен)
            (rating nc-17)
            (category гет)
            (original-text нет)
            (tags au ангст вымышленные-существа драма насилие романтика)
    )
    (fanfic (name "Seduction")
            (author "Mr Abomination")
            (description "...Стиву Рема жалко не было, ни когда он не хуже остальных кидал в парнишку шариками скомканной бумаги, ни когда участвовал в его избиении, ни даже когда окунал его головой в школьный сортир – правда, было это еще в восьмом классе. Стиву не было жалко Рема, потому что не оставалось сил пожалеть даже себя самого...")
            (finish-date "30.03.2013")
            (pages 27)
            (fandom ориджинал)
            (status завершен)
            (rating nc-17)
            (category слэш)
            (original-text нет)
            (tags pwp любовь-ненависть повседневность счастливый-финал учебные-заведения эмо юмор)
    )
)
